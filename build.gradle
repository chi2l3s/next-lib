plugins {
    id 'java'
    id 'maven-publish'
}

group = 'io.github.chi2l3s'
version = '1.0.7'

repositories {
    mavenCentral()
    maven { url = "https://repo.papermc.io/repository/maven-public/" }
    maven {
        url = 'https://repo.extendedclip.com/releases/'
    }
}

dependencies {
    compileOnly "com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT"

    compileOnly "org.projectlombok:lombok:1.18.34"
    annotationProcessor "org.projectlombok:lombok:1.18.34"
    compileOnly 'me.clip:placeholderapi:2.11.6'

    implementation 'com.tchristofferson:ConfigUpdater:2.2'
    implementation 'org.yaml:snakeyaml:2.2'
    implementation 'com.squareup:javapoet:1.13.0'
    implementation 'com.zaxxer:HikariCP:7.0.2'

    // Testing dependencies
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.10.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
    testImplementation 'org.mockito:mockito-core:5.8.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.8.0'
    testImplementation 'org.assertj:assertj-core:3.25.1'
    testImplementation 'com.h2database:h2:2.2.224'
    testImplementation 'org.xerial:sqlite-jdbc:3.45.0.0'
    testImplementation "com.destroystokyo.paper:paper-api:1.16.5-R0.1-SNAPSHOT"
    testCompileOnly "org.projectlombok:lombok:1.18.34"
    testAnnotationProcessor "org.projectlombok:lombok:1.18.34"
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.github.chi2l3s'
            artifactId = 'nextlib'
            version = version
            from components.java

            pom {
                name = 'NextLib'
                description = 'A comprehensive library for Minecraft Paper/Spigot plugins'
                url = 'https://github.com/chi2l3s/next-lib'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'chi2l3s'
                        name = 'chi2l3s'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/chi2l3s/next-lib.git'
                    developerConnection = 'scm:git:ssh://github.com/chi2l3s/next-lib.git'
                    url = 'https://github.com/chi2l3s/next-lib'
                }
            }
        }
    }
    repositories {
        maven {
            url = "http://localhost:8081/nexus/content/repositories/nextrepo/"
            allowInsecureProtocol = true
            credentials {
                username = 'admin'
                password = 'admin123'
            }
        }
    }
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}
